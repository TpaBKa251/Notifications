plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.1'
    id "org.sonarqube" version "7.0.1.6134"
    id 'jacoco'
}

group = 'ru.tpu.hostel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

def repoUser = project.findProperty("privateRepoUsername") ?: System.getenv("INTERNAL_REPO_LOGIN")
def repoPass = project.findProperty("privateRepoPassword") ?: System.getenv("INTERNAL_REPO_PASSWORD")

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven {
        url "http://82.202.138.26:8081/repository/internal/"
        allowInsecureProtocol = true
        credentials {
            username = repoUser
            password = repoPass
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "TpaBKa251_Notifications"
        property "sonar.organization", "hostel-service" // Замените на имя вашей организации
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_TOKEN") // Используется токен из секретов GitHub
        property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
        property "sonar.coverage.exclusions", "**/*Generated*.java,**/model/*.java" // Исключения из анализа
        property "sonar.java.binaries", "build/classes/java/main" // Путь к скомпилированным классам
    }
}

dependencyManagement {
    imports {
        mavenBom 'io.grpc:grpc-bom:1.56.1'
    }
}

ext {
    set('internalLibraryVersion', "1.5.5")
}

dependencies {
    implementation "ru.tpu.hostel:hostel-core:${internalLibraryVersion}"

    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2023.0.2")
    //implementation 'ch.qos.logback:logback-core:1.5.13'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    runtimeOnly 'org.postgresql:postgresql:42.7.8'
    implementation 'org.flywaydb:flyway-core:11.17.2'
    implementation 'org.flywaydb:flyway-database-postgresql:11.17.2'
    implementation 'org.ehcache:ehcache:3.10.8:jakarta'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    implementation 'com.google.firebase:firebase-admin:9.7.0'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'com.google.protobuf:protobuf-java:4.33.1'
    implementation 'io.grpc:grpc-protobuf:1.56.1'
    implementation 'io.netty:netty-common:4.1.118.Final'

    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.49.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'
    implementation 'com.github.loki4j:loki-logback-appender:1.5.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
    implementation 'io.opentelemetry.contrib:opentelemetry-samplers:1.49.0-alpha'
    implementation 'io.opentelemetry:opentelemetry-sdk-extension-autoconfigure-spi:1.49.0'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-jdbc:2.22.0-alpha'


    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.instancio:instancio-core:2.13.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Генерируем отчет после тестов
}

jacoco {
    toolVersion = "0.8.14" // Версия JaCoCo
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true) // Включаем XML для SonarCloud
        html.required.set(false)
        csv.required.set(false)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.tpu.hostel.notifications.NotificationsApplication'
    }
}
